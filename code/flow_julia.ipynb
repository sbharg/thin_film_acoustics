{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Custom struct for topogrpahy functions\n",
    "\"\"\"\n",
    "struct Topography\n",
    "    f::Function\n",
    "    params::Tuple\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Function describing a flat plane\n",
    "\"\"\"\n",
    "function flat(x)\n",
    "    0\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Function describing a bump\n",
    "# Arguments\n",
    "- `x`: x coordinate\n",
    "- `w`: width scaling\n",
    "- `c`: x coordinate for the center of the bump\n",
    "- `h`: optional arg for height scaling\n",
    "\"\"\"\n",
    "function bump(x, w, c, h=1)\n",
    "    if x <= c-w || x >= c+w\n",
    "        return 0\n",
    "    else\n",
    "        return h * exp(-(w^2/(w^2-(x-c)^2)))\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Function describing a rectangular mound\n",
    "# Arguments\n",
    "- `x`: x coordinate\n",
    "- `c`: x coordinate for left hand corner of mound\n",
    "- `d`: height of mound\n",
    "- `delta`: steepness of mound\n",
    "- `w`: width of mound\n",
    "\"\"\"\n",
    "function mound(x, c, h, delta, w)\n",
    "    h * (1/pi * (atan((x-c)/delta) - atan((x-c-w)/delta)))\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Function describing a reflected sigmoid\n",
    "# Arguments\n",
    "- `x`: x coordinate\n",
    "- `b`: precursor film height\n",
    "- `xf`: center point of transition\n",
    "- `c`: steepness of transition\n",
    "\"\"\"\n",
    "function reflected_sigmoid(x, b, xf, c)\n",
    "    b + (1-b)/(1 + exp(1/c * (x - xf)))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Builds the system of ODEs (represented by du)\n",
    "u = (h_-1, h_0, ..., h_nx, h_nx+1) where h_-1 and h_nx+1 are ghost points\n",
    "\"\"\"\n",
    "function system!(du, u, p, t)\n",
    "    D, alpha, dx, topo, params = p\n",
    "\n",
    "    function phi(i)\n",
    "        u[i] + topo(dx*(i-2), params...)\n",
    "    end\n",
    "    function interp(i)\n",
    "        1/2 * (u[i]^3 + u[i+1]^3)\n",
    "    end\n",
    "    function f1(i)\n",
    "        1/(dx^2) * (interp(i-1)*(phi(i-1) - phi(i)) + interp(i)*(phi(i+1) - phi(i)))\n",
    "    end\n",
    "    function f2(i)\n",
    "        (1/dx^4) * (interp(i-1)*(phi(i-2) - 3*phi(i-1) + 3*phi(i) - phi(i+1)) + interp(i)*(-phi(i-1) + 3*phi(i) - 3*phi(i+1) + phi(i+2)))\n",
    "    end\n",
    "    function f3(i)\n",
    "        (1/(2*dx)) * (u[i+1]^3 - u[i-1]^3)\n",
    "    end\n",
    "\n",
    "    du[1] = 0\n",
    "    du[2] = 0\n",
    "    du[end-1] = 0\n",
    "    du[end] = 0\n",
    "    for i in 3:length(u)-2\n",
    "        du[i] = D*cos(alpha)*f1(i) - f2(i) - sin(alpha)*f3(i)\n",
    "    end\n",
    "    return du\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Discretization of x-domain\n",
    "nx = 800\n",
    "Lx = 40\n",
    "dx = Lx/nx\n",
    "x = 0.0:dx:Lx\n",
    "\n",
    "# Topography definition\n",
    "bcenter = 20.0\n",
    "bscale = 5.0\n",
    "topo = Topography(bump, (bscale, bcenter))\n",
    "#topo = Topography(flat, ())\n",
    "s = topo.f.(x, topo.params...)\n",
    "\n",
    "# Initial condition definition\n",
    "b = 0.01\n",
    "xf = 5\n",
    "steep = 0.5\n",
    "ic = reflected_sigmoid.(x, b, xf, steep)\n",
    "# Adding ghost points\n",
    "pushfirst!(ic, ic[1])\n",
    "push!(ic, ic[end])\n",
    "\n",
    "# Parameters of the system of ODEs\n",
    "D = 1\n",
    "alpha = pi/4\n",
    "\n",
    "xsym = Symbol.(x)\n",
    "pushfirst!(xsym, Symbol(\"ghost1\"))\n",
    "push!(xsym, Symbol(\"ghost2\"))\n",
    "\n",
    "# Solve the system of ODEs\n",
    "using DifferentialEquations\n",
    "\n",
    "p = (D, alpha, dx, topo.f, topo.params)\n",
    "tspan = (0.0, 40.0); save_every = 0.5\n",
    "f = ODEFunction(system!, syms=xsym)\n",
    "prob = ODEProblem(f, ic, tspan, p, saveat=save_every)\n",
    "sol = solve(prob, alg=Rodas4())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using Plots, LaTeXStrings\n",
    "Plots.PyPlotBackend()\n",
    "\n",
    "plot_font = \"Computer Modern\"\n",
    "default(fontfamily=plot_font, grid=false, palette = :berlin)\n",
    "\n",
    "# Controls the time step between fluid profile plotting\n",
    "time_skip = 2\n",
    "dt = floor(Int8, time_skip/save_every)\n",
    "\n",
    "plt = plot(x, s, dpi=200, xlabel=L\"Dimensionless length $x$\", ylabel=L\"Dimensionless height $h$\", legend=false, color=\"black\",\n",
    "        title=L\"Fluid profile at $\\delta t = %$time_skip$ intervals\", ylims=(0, Inf), xlims=(0, Lx))\n",
    "for i = 1:floor(Int8, tspan[2]/time_skip)+1\n",
    "    plot!(plt, x, sol.u[1 + dt*(i-1)][2:end-1] + s)\n",
    "end\n",
    "\n",
    "# Animates fluid profile based in time steps sepcified in ODE solver\n",
    "anim = @animate for i = 1:size(sol.u)[1]\n",
    "    plot(x, s, color=\"black\", xlabel=L\"Dimensionless length $x$\", ylabel=L\"Dimensionless height $h$\")\n",
    "    plot!(x, sol.u[i][2:end-1]+s, legend=false, ylims=(0, 2), xlims=(0, Lx))\n",
    "end\n",
    "\n",
    "using Dates, CSV, DataFrames\n",
    "\n",
    "run_tag = string(Dates.format(now(), \"YYYYmmdd-HHMMSS\"))\n",
    "path = \"../runs/$run_tag\"\n",
    "\n",
    "mkpath(path)\n",
    "gif(anim, path * \"/flow.gif\", fps = 5)\n",
    "savefig(plt, path * \"/plt.png\")\n",
    "df = DataFrame(sol)\n",
    "#=\n",
    "CSV format\n",
    "First Col - Timestamp\n",
    "Rest of Cols - Data including ghost points\n",
    "=#\n",
    "CSV.write(path * \"/data.csv\", df)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.0",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.0"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
